# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.0.4
pkgrel=1
pkgdesc="A library to extract, create, and modify WIM files"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/wimlib"
license=("GPL3")
depends=("openssl" "fuse" "libxml2" "ntfs-3g" "attr")
optdepends=("cdrkit: for making ISO image of Windows PE"
	"mtools: for making disk image of Windows PE"
	"syslinux: for making disk image of Windows PE"
	"cabextract: for extracting Windows PE from the WAIK"
	"ntfsprogs: for making NTFS filesystems")
checkdepends=("ntfsprogs")
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")

build() {
	cd "$pkgname-$pkgver"
	./configure --disable-verify-compression --with-libcrypto --with-fuse \
		    --with-ntfs-3g --enable-xattr --prefix=/usr
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}

sha512sums=(4981b8e88df34193f0753edaf7c6800bf5bdbb34ddb44824220f4104f7e5710a4171de2ebf613ba80db45d7c474ac19171934076eed2cca423c9ecf3c8b0582b)
